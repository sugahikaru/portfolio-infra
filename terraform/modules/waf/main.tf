# WAF Web ACL
resource "aws_wafv2_web_acl" "main" {
  name  = "WebACL"
  scope = "REGIONAL"

  default_action {
    allow {}
  }

  visibility_config {
    cloudwatch_metrics_enabled = true
    metric_name                = "WAFmetric"
    sampled_requests_enabled   = true
  }

  rule {
    name     = "AWS-AWSManagedRulesCommonRuleSet"
    priority = 0

    override_action {
      none {}
    }

    statement {
      managed_rule_group_statement {
        name        = "AWSManagedRulesCommonRuleSet"
        vendor_name = "AWS"
      }
    }

    visibility_config {
      cloudwatch_metrics_enabled = true
      metric_name                = "CoreRules"
      sampled_requests_enabled   = true
    }
  }

  tags = {
    Name = "WebACL"
  }
}

# WAF と ALB の関連付け
resource "aws_wafv2_web_acl_association" "main_association" {
  resource_arn = var.alb_arn
  web_acl_arn  = aws_wafv2_web_acl.main.arn
}

# CloudWatchロググループ作成
resource "aws_cloudwatch_log_group" "waf_log_group" {
  name              = "aws-waf-logs-aws-study"
  retention_in_days = 14

  tags = {
    Name = "MyWAF-Log"
  }
}

# WAF ログ設定
resource "aws_wafv2_web_acl_logging_configuration" "waf_logging" {
  resource_arn = aws_wafv2_web_acl.main.arn

  log_destination_configs = [
    aws_cloudwatch_log_group.waf_log_group.arn
  ]
}

